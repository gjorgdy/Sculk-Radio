plugins {
    id 'fabric-loom' version "${loom_version}"
    id 'maven-publish'
    id "com.modrinth.minotaur" version "2.+"
}

var min_minecraft_version = project.minecraft_versions.split(',').first()
version = "${project.version}+${min_minecraft_version}"
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    maven { url = 'https://maven.maxhenkel.de/repository/public' }
    maven {
        url = 'https://api.modrinth.com/maven'
        content {
            includeGroup "maven.modrinth"
        }
    }
    maven { url = 'https://maven.parchmentmc.org/' }
    maven { url 'https://maven.fallenbreath.me/releases' }
    maven {
        name = "Local Maven"
        url = uri('../local-maven')
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    // Plugin for conditional mixins
    modImplementation 'me.fallenbreath:conditional-mixin-fabric:0.6.4'
    include 'me.fallenbreath:conditional-mixin-fabric:0.6.4'
    // Audio Player compatibility
    modRuntimeOnly "de.maxhenkel.voicechat:voicechat-api:${project.voicechat_api_version}:fabric-stub"
    modRuntimeOnly "maven.modrinth:simple-voice-chat:fabric-${project.voicechat_mod_version}"
    modCompileOnly "de.maxhenkel.voicechat:voicechat-api:${project.voicechat_api_version}"
    modImplementation "de.maxhenkel.audioplayer:audioplayer:${project.audioplayer_mod_version}" // no maven available, build to local maven to use
}

processResources {
    inputs.property "version", version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": version,
                "minecraft_version": project.minecraft_version,
                "min_minecraft_version": min_minecraft_version,
                "loader_version": project.loader_version,
                "fabric_version": project.fabric_version,
                "voicechat_api_version": project.voicechat_api_version,
                "audioplayer_api_version": project.audioplayer_api_version
    }
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }
    repositories {
        maven {
            url = uri('../local-maven')
        }
    }
}

static String getModrinthToken() {
    return new File('.env')
            .readLines()
            .findAll { it?.trim() && !it.trim().startsWith('#') }
            .collect { it.split('=') }
            .find { it[0] == 'MODRINTH_TOKEN' }[1]
}

tasks.modrinth.dependsOn(tasks.modrinthSyncBody)
modrinth {
    token = getModrinthToken()
    projectId = project.mod_id
    versionNumber = "${version}"
    versionName = "${project.name} ${version}"
    changelog = rootProject.file("changelog.md").text
    syncBodyFrom = rootProject.file("README.md").text
    versionType = project.mod_release_type
    uploadFile = remapJar
    gameVersions = project.minecraft_versions.split(',') as List<String>
    loaders = ["fabric"]
    dependencies {
        required.project "fabric-api"
        optional.project "audioplayer"
    }
}

